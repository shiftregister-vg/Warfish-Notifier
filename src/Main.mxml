<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   creationComplete="onCreationComplete()">
	
	<fx:Script>
		<![CDATA[
			import flash.display.NativeMenuItem;
			import flash.events.Event;
			import flash.media.Sound;
			import flash.media.SoundChannel;
			import flash.media.SoundMixer;
			import flash.media.SoundTransform;
			import flash.net.URLLoader;
			import flash.net.URLRequest;
			import flash.net.URLRequestMethod;
			import flash.net.navigateToURL;
			import flash.utils.clearInterval;
			import flash.utils.setInterval;
			
			import managers.IconMenuManager;
			import managers.UpdateManager;
			
			import mx.events.AIREvent;
			import mx.states.AddItems;
			
			import views.AlertBubble;
			import views.ConfigWindow;
			
			import vo.WarfishConfig;
			
			private var configWindow:ConfigWindow;
			private var icon:Loader = new Loader();
			private var iconMenu:NativeMenu = new NativeMenu();
			private var interval:int;
			private var bubbleInterval:int;
			private var rssXML:XML;
			[Bindable][Embed(source="/assets/sound/take-your-damn-warfish-turn.mp3")]
			private var alertSoundClass:Class;
			private var soundEmbed:Sound = new alertSoundClass() as Sound;
			private var soundChannel:SoundChannel;
			private var iconMenuManager:IconMenuManager = new IconMenuManager(iconMenu);
			
			[Bindable] private var warfishConfig:WarfishConfig = new WarfishConfig();
			
			private function onCreationComplete():void{
				setAutoLoad();
				NativeApplication.nativeApplication.autoExit = false;
				
				iconMenuManager.warfishConfig = warfishConfig;
				iconMenuManager.addEventListener("Exit",onExitSelect);
				iconMenuManager.addEventListener("Configure",onConfigSelect);
				iconMenuManager.addEventListener("Enable Sound",onSoundSelect);
				iconMenuManager.addEventListener("Open Warfish",onWebsiteSelect);
				iconMenuManager.buildIconMenu();
				
				setIcon();
				startInterval();
				
				if (!warfishConfig.rssURL.length){
					onConfigSelect();
				}
				
				var updateManager:UpdateManager = new UpdateManager();
					updateManager.manifestURL = "http://update.slantsoft.com/warfishnotification/manifest.xml";
					updateManager.checkForUpdate();
			}
			
			private function setAutoLoad():void{
				try {
					NativeApplication.nativeApplication.startAtLogin = warfishConfig.startOnLogin;
				} catch (e:Error) {}
			}
			
			private function iconLoadComplete(event:Event):void {
				NativeApplication.nativeApplication.icon.bitmaps =
					[event.target.content.bitmapData];
			}
			
			private function onWebsiteSelect(event:Event=null):void{
				navigateToURL(new URLRequest("http://warfish.net/war/play/"));
			}
			
			private function onSoundSelect(event:Event=null):void{
				var menuItem:NativeMenuItem = event.target as NativeMenuItem;
				warfishConfig.playSound = menuItem.checked = !menuItem.checked;
			}
			
			private function onConfigSelect(event:Event=null):void{
				if (!configWindow || configWindow.closed){
					configWindow = new ConfigWindow();
				}
				
				configWindow.warfishConfig = warfishConfig;
				configWindow.addEventListener(Event.CLOSE,onConfigWindowClose);
				configWindow.open();
				configWindow.activate();
			}
			
			private function onConfigWindowClose(event:Event):void{
				setAutoLoad();
				startInterval();
			}
			
			private function onExitSelect(event:Event):void{
				NativeApplication.nativeApplication.icon.bitmaps = [];
				NativeApplication.nativeApplication.exit();
			}
			
			private function startInterval():void{
				if (interval){
					clearInterval(interval);
				}
				if (warfishConfig.rssURL.length){
					interval = setInterval(onTick,30000);
				}
				if (bubbleInterval){
					clearInterval(bubbleInterval);
					bubbleInterval = 0;
					bubbleInterval = setInterval(showAlert,warfishConfig.bubbleInterval);
				}
			}
			
			private function onTick():void{
				var urlRequest:URLRequest = new URLRequest(warfishConfig.rssURL);
					urlRequest.contentType = "text/xml";
					urlRequest.method = URLRequestMethod.GET;
				var loader:URLLoader = new URLLoader();
					loader.addEventListener(Event.COMPLETE,onRequestComplete);
					loader.load(urlRequest);
			}
			
			private function onRequestComplete(event:Event):void{
				var loader:URLLoader = URLLoader(event.target);
				rssXML = XML(loader.data);
				
				var isTurn:Boolean = false;
				if (rssXML..item.length() > 1){
					isTurn = true;
				}
				
				if (!isTurn){
					setIcon();
					iconMenuManager.removeMenuByLabel("Games");
					if (bubbleInterval){
						clearInterval(bubbleInterval);
					}
				} else {
					var gamesMenuItem:NativeMenuItem = new NativeMenuItem("Games");
					var gamesMenu:NativeMenu = new NativeMenu();
					for (var i:int = 0; i < rssXML..item.length(); i++){
						if (rssXML..item[i].title.toString() != "View Your Turn Games list"){
							var gameMenu:NativeMenuItem = gamesMenu.addItem(new NativeMenuItem(rssXML..item[i].title.toString().replace(" [Your turn]","")));
							gameMenu.data = {url:rssXML..item[i].link.toString()};
							gameMenu.addEventListener(Event.SELECT,function(event:Event):void{
								navigateToURL(new URLRequest(event.target.data.url));
							});
						}
					}
					gamesMenuItem.submenu = gamesMenu;
					iconMenuManager.addMenuItemAt(gamesMenuItem,1);
					
					setIcon("-red"," - It's your turn!");
					if (!bubbleInterval){
						showAlert();
						bubbleInterval = setInterval(showAlert,warfishConfig.bubbleInterval);
					}
				}
			}
			
			private function setIcon(iconName:String="",tooltip:String=""):void{
				if (NativeApplication.supportsSystemTrayIcon) {
					NativeApplication.nativeApplication.autoExit = false;
					icon.contentLoaderInfo.addEventListener(Event.COMPLETE, iconLoadComplete);
					icon.load(new URLRequest("assets/icon/warfish" + iconName + "-16.png"));
					
					var systray:SystemTrayIcon = NativeApplication.nativeApplication.icon as SystemTrayIcon;
					systray.tooltip = "Warfish Notifier" + tooltip;
					systray.menu = iconMenu;
				}
				
				if (NativeApplication.supportsDockIcon){
					icon.contentLoaderInfo.addEventListener(Event.COMPLETE,iconLoadComplete);
					icon.load(new URLRequest("assets/icon/warfish" + iconName + "-128.png"));
					var dock:DockIcon = NativeApplication.nativeApplication.icon as DockIcon; 
					dock.menu = iconMenu;
				}
			}
			
			private function playSound(event:Event=null):void{
				if (warfishConfig.playSound){
					SoundMixer.soundTransform = new SoundTransform(1);
					soundChannel = soundEmbed.play();
				}
			}
			
			private function showAlert():void{
				if (bubbleInterval){
					clearInterval(bubbleInterval);
				}
				var alertBubble:AlertBubble = new AlertBubble();
					alertBubble.warfishConfig = warfishConfig;
					alertBubble.addEventListener(AIREvent.WINDOW_ACTIVATE,playSound);
					alertBubble.open();
					alertBubble.activate();
			}
		]]>
	</fx:Script>
	<fx:Declarations>
	</fx:Declarations>
</s:Application>
